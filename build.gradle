plugins {
    id 'java'
    id 'maven-publish'
}

group = "de.cyklon"
version = "1.0.0"

var commit = getCommit()
var release = System.getenv("RELEASE") == "true"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter"

    implementation 'net.dv8tion:JDA:5.0.0-beta.21'
    implementation 'com.discord4j:discord4j-core:3.2.5'
    implementation 'org.javacord:javacord:3.8.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://maven.cyklon.dev/" + (release ? "releases" : "snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "de.cyklon"
            artifactId = "DiscordChatExporter"
            version = release ? this.version : commit

            from components.java
        }
    }
}

tasks.test {
    useJUnitPlatform()
}

static String getCommit() {
    try {
        var commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        return commit.isEmpty() ? 'unknown' : commit
    } catch(IOException e) {
        throw new RuntimeException(e)
    }
}

